apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 30
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionName '0.5.1'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'org.robolectric:robolectric:4.6.1'

    androidTestImplementation 'androidx.test:core-ktx:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
    androidTestImplementation 'io.mockk:mockk-android:1.12.0'
}

tasks.findAll { it.name.contains("dokka") }.forEach {
    it.configure {
        moduleName.set("Dreams")
        moduleVersion.set(android.defaultConfig.versionName)
        dokkaSourceSets {
            named("main") {
                includes.from("module.md")
                sourceLink {
                    localDirectory.set(file("src/main/java"))
                    remoteUrl.set(URL("https://github.com/getdreams/dreams-android-sdk/tree/master/sdk/"))
                    remoteLineSuffix.set("#L")
                }
                includeNonPublic.set(false)
                noAndroidSdkLink.set(false)
                reportUndocumented.set(true)
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task javadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc
}
task htmlJar(type: Jar, dependsOn: dokkaHtml) {
    archiveClassifier.set("html-doc")
    from dokkaHtml
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/getdreams/dreams-android-sdk")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.getdreams'
                artifactId = 'android-sdk'
                version = android.defaultConfig.versionName

                artifact androidSourcesJar
                artifact javadocsJar
                artifact htmlJar

                pom {
                    name = 'Dreams Android SDK'
                    description = ''
                    url = 'https://getdreams.com/'

                    licenses {
                        license {
                            name = 'Mozilla Public License Version 2.0'
                            url = 'http://mozilla.org/MPL/2.0/'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/dreams-android-sdk.git'
                        developerConnection = 'scm:git:ssh://github.com/dreams-android-sdk.git'
                        url = 'https://getdreams.com/'
                    }
                }

                signing {
                    useGpgCmd()
                    sign publishing.publications.release
                }
            }
        }
    }
}
